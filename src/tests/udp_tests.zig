const std = @import("std");
const c = @import("c");
const xev = @import("xev");
const support = @import("test_support");

const Router = support.radix.Router;

const FakeEngine = struct {
    const Self = @This();

    expected_address: ?*Self = null,
    start_count: usize = 0,

    pub fn init() !Self {
        return .{};
    }

    pub fn start(
        self: *Self,
        _: *c.SSL_CTX,
        _: *const Router,
        _: std.posix.socket_t,
        _: std.Io.net.IpAddress,
    ) !void {
        self.start_count += 1;
        if (self.expected_address != self) return error.EngineMoved;
        return error.TestStop;
    }

    pub fn deinit(_: *Self) void {}

    pub fn process_datagram(_: *Self, _: []const u8, _: std.Io.net.IpAddress) void {}

    pub fn process(_: *Self) void {}

    pub fn next_timeout_ms(_: *const Self) u64 {
        return 1000;
    }

    pub fn cooldown(_: *Self) void {}
};

test "udp: QUIC engine starts only after transport reaches stable storage" {
    const Transport = support.udp.quic_transport(FakeEngine);
    var router = Router.init();
    const ssl_ctx: *c.SSL_CTX = @ptrFromInt(1);
    var transport = try Transport.init(ssl_ctx, &router, "127.0.0.1", 0);
    defer transport.deinit();

    try std.testing.expectEqual(@as(usize, 0), transport.engine.start_count);
    transport.engine.expected_address = &transport.engine;

    var loop: xev.Loop = undefined;
    try std.testing.expectError(error.TestStop, transport.start(&loop));
    try std.testing.expectEqual(@as(usize, 1), transport.engine.start_count);
}
